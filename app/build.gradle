apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.sanqian.newd6.sgjh"
        minSdkVersion 15
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        flavorDimensions "versioncode"
        ndk {
            abiFilters 'armeabi','armeabi-v7a','arm64-v8a','x86'
        }
    }
    //解决冲突
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    sourceSets.main{
        jniLibs.srcDir 'libs'
    }

    signingConfigs {
        config {
            keyAlias 'key0'
            keyPassword '123456789'
            storeFile file('../d6.jks')
            storePassword '123456789'
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }

        //未签名版release编译，用于给渠道出母包
        release_unsigned {
            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }

    sourceSets {
        inner_test {
            assets.srcDirs = ['../assets']
//            jniLibs.srcDirs = ['libs']
        }
        outer_test {
            assets.srcDirs = ['../assets']
            jniLibs.srcDirs = ['libs']
        }
        shoubin_fengche {
            manifest.srcFile '../extra/shoubin_fengche_AndroidManifest.xml'
            assets.srcDirs = ['../extra/shoubin_fengche_assets']
            jniLibs.srcDirs = ['../extra/shoubin_fengche_libs']
            res.srcDirs = ['../extra/shoubin_fengche_res']
        }
    }

    productFlavors {
        //内网测试包
        inner_test {
            manifestPlaceholders = [
                    //applicationId "com.guangzhouqianyou.sanqian.d6"
                    //versionCode 1
                    //versionName "1.0"
                    CHANNEL_ID: "inner_test",
                    app_name:"三国将魂内网测试",
                    icon:"@mipmap/ic_launcher",
                    sdk_name:"none",
                    index_version:"http://192.168.1.204/index.html"
            ]
        }
        //外网测试包
        outer_test {
//            applicationId "com.guangzhouqianyou.sanqian.d6"
            manifestPlaceholders = [
                    CHANNEL_ID: "outer_test",
                    app_name:"三国将魂外网测试",
                    icon:"@mipmap/ic_launcher",
                    sdk_name:"none",
                    index_version:"http://play.dev.idlestack.com/newd6/android/index.html"
            ]
        }

        //手柄，风车
        shoubin_fengche {
            applicationId "com.sanguojh_q.game"
            manifestPlaceholders = [
                    CHANNEL_ID: "shoubin_fengche",
                    app_name:"三国将魂",
                    icon:"@mipmap/ic_launcher",
                    sdk_name:"QYSDK",
                    index_version:"https://resource.jieyougame.com/newd6/qianyou_android_ttgame/"
            ]
        }
    }

    buildToolsVersion '26.0.2'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            def date = new Date()
            def formattedDate = date.format('yyyyMMddHHmmss')
            if (outputFile != null && outputFileName.endsWith('.apk')){
                outputFileName = "sanguo-${productFlavors.name}-${versionName}-${buildType.name}-${formattedDate}.apk"
            }
        }
    }
}

def channel = ""
gradle.startParameter.getTaskNames().each { task ->
    if (task.toLowerCase().contains("shoubin_fengche")) {
        channel = "shoubin_fengche"
    }
}

dependencies {
    implementation(name: 'egret', ext: 'aar')
//    为方便sdk的接入开发，过程中条件控制语句暂时屏蔽，当接入完成，打开条件控制语句，忽略代码提示的报错，不影响出包
//    if (channel == "shoubin_fengche"){
        implementation fileTree(dir: '../extra/shoubin_fengche_libs', include: ['*.jar'])
//    }
}
